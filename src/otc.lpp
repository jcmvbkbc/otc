%{
#include <vector>
#include <stdio.h>
#include "otc-otc.hpp"
#include "otc.h"
%}

%option nodebug

Whitespace [ \t\v]+

DecimalLiteral [1-9][0-9]*
IntegerLiteral "0"|{DecimalLiteral}

SizedLiteral {DecimalLiteral}"'"[bdh][0-9a-fA-FxX_]+

Letter [a-zA-Z_]
Digit [0-9]
IdentifierSpecial [.]

Identifier {Letter}({Letter}|{Digit}|{IdentifierSpecial})*
AdditiveOperator [+-]
EqualityOperator "=="|"!="
RelationalOperator "<"|"<="|">"|">="
BitwiseXorOperator "^"|"^~"|"~^"
ShiftOperator "<<"|">>"
Separator [{}()?:;,*!=|~&]|"["|"]"
Stage "Rstage"|"Estage"|"Mstage"|"Wstage"

StringLiteral "\""[^\"]*"\""

%x comment line_comment

%%
"/*"				BEGIN(comment);
<comment>[^*\n]*		/* eat anything that's not a '*' */
<comment>"*"+[^*/\n]*		/* eat up '*'s not followed by '/'s */
<comment>"*"+"/"		BEGIN(INITIAL);
"//"				BEGIN(line_comment);
<line_comment>[^\n]*		/* eat anything that's not a line end */
<line_comment>\n		{ Location::current.m_line++; BEGIN(INITIAL); }

<INITIAL,comment>\n+		{ Location::current.m_line += yyleng; }
<*>\r+

"||"				{ return TOKEN_LOGICAL_OR_OPERATOR; }
"wire"				{ return TOKEN_WIRE; }
"view"				{ return TOKEN_VIEW; }
"user_register"			{ return TOKEN_USER_REGISTER; }
"use"				{ return TOKEN_USE; }
"table"				{ return TOKEN_TABLE; }
"synopsis"			{ return TOKEN_SYNOPSIS; }
"state"				{ return TOKEN_STATE; }
"specialized_op"		{ return TOKEN_SPECIALIZED_OP; }
"slot_shared"			{ return TOKEN_SLOT_SHARED; }
"slot_opcodes"			{ return TOKEN_SLOT_OPCODES; }
"slot"				{ return TOKEN_SLOT; }
"shared_semantic"		{ return TOKEN_SHARED_SEMANTIC; }
"shared_or"			{ return TOKEN_SHARED_OR; }
"shared"			{ return TOKEN_SHARED; }
"semantic"			{ return TOKEN_SEMANTIC; }
"schedule"			{ return TOKEN_SCHEDULE; }
"regfile"			{ return TOKEN_REGFILE; }
"reference"			{ return TOKEN_REFERENCE; }
"rdy"				{ return TOKEN_RDY; }
"queue"				{ return TOKEN_QUEUE; }
"proto"				{ return TOKEN_PROTO; }
"property"			{ return TOKEN_PROPERTY; }
"port"				{ return TOKEN_PORT; }
"package"			{ return TOKEN_PACKAGE; }
"out"				{ return TOKEN_OUT; }
"operator"			{ return TOKEN_OPERATOR; }
"operation"			{ return TOKEN_OPERATION; }
"operand_sem"			{ return TOKEN_OPERAND_SEM; }
"operand_map"			{ return TOKEN_OPERAND_MAP; }
"operand_hw_decode"		{ return TOKEN_OPERAND_HW_DECODE; }
"operand"			{ return TOKEN_OPERAND; }
"opcode"			{ return TOKEN_OPCODE; }
"no_bypass"			{ return TOKEN_NO_BYPASS; }
"lookup"			{ return TOKEN_LOOKUP; }
"length"			{ return TOKEN_LENGTH; }
"interface"			{ return TOKEN_INTERFACE; }
"inout"				{ return TOKEN_INOUT; }
"in"				{ return TOKEN_IN; }
"import_wire"			{ return TOKEN_IMPORT_WIRE; }
"immediate_range"		{ return TOKEN_IMMEDIATE_RANGE; }
"immediate"			{ return TOKEN_IMMEDIATE; }
"imap"				{ return TOKEN_IMAP; }
"iclass"			{ return TOKEN_ICLASS; }
"function"			{ return TOKEN_FUNCTION; }
"format"			{ return TOKEN_FORMAT; }
"field"				{ return TOKEN_FIELD; }
"export"			{ return TOKEN_EXPORT; }
"endpackage"			{ return TOKEN_END_PACKAGE; }
"enTIEcomp"			{ return TOKEN_EN_TIE_COMP; }
"enFLIX"			{ return TOKEN_EN_FLIX; }
"description"			{ return TOKEN_DESCRIPTION; }
"default"			{ return TOKEN_DEFAULT; }
"def"				{ return TOKEN_DEF; }
"data_gate"			{ return TOKEN_DATA_GATE; }
"ctype"				{ return TOKEN_CTYPE; }
"cstub_swap"			{ return TOKEN_CSTUB_SWAP; }
"core"				{ return TOKEN_CORE; }
"coprocessor"			{ return TOKEN_COPROCESSOR; }
"assign"			{ return TOKEN_ASSIGN; }
"add_read_write"		{ return TOKEN_ADD_READ_WRITE; }
"InstBuf"			{ return TOKEN_INST_BUF; }
"->"				{ return TOKEN_ARROW; }
"&&"				{ return TOKEN_AND; }

{AdditiveOperator}		{ return TOKEN_ADDITIVE_OPERATOR; }
{EqualityOperator}		{ return TOKEN_EQUALITY_OPERATOR; }
{RelationalOperator}		{ return TOKEN_RELATIONAL_OPERATOR; }
{BitwiseXorOperator}		{ return TOKEN_BITWISE_XOR_OPERATOR; }
{ShiftOperator}			{ return TOKEN_SHIFT_OPERATOR; }
{Stage}				{ return TOKEN_STAGE_NAME; }
{Identifier}			{ return TOKEN_IDENTIFIER; }
{IntegerLiteral}		{ return TOKEN_NUMBER; }
{SizedLiteral}			{ return TOKEN_SIZED_NUMBER; }
{Separator}			{ return *yytext; }
{StringLiteral}			{ return TOKEN_STRING_LITERAL; }

{Whitespace}

%%
